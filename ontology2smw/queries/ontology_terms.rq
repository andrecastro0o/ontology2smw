PREFIX dc: <http://purl.org/dc/elements/1.1/title>
PREFIX dcterm: <http://purl.org/dc/terms/>
PREFIX ns: <http://www.w3.org/2003/06/sw-vocab-status/ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX aeon: <https://github.com/tibonto/aeon#>
PREFIX iao: <http://purl.obolibrary.org/obo/>

SELECT DISTINCT ?term ?termType ?label ?range ?subclassof ?subpropertyof ?definition
WHERE {
    {   
        ?term rdf:type owl:Class. 
        BIND (owl:Class as ?termType).
        OPTIONAL {
            ?term rdfs:subClassOf ?subclassof.
            FILTER (!isBlank(?subclassof))
        }
    }
    UNION
    {    
        ?term rdf:type owl:DatatypeProperty.
        BIND (owl:DatatypeProperty as ?termType).
        OPTIONAL {
            ?term rdfs:subPropertyOf ?subpropertyof.
            FILTER (!isBlank(?subpropertyof))
        }
        OPTIONAL{ ?term rdfs:range ?range. }
    }
    UNION
    {      
        ?term rdf:type owl:ObjectProperty.
        BIND (owl:ObjectProperty as ?termType).
        OPTIONAL {
            ?term rdfs:subPropertyOf ?subpropertyof.
            FILTER (!isBlank(?subpropertyof))
        }
    }
    # Leaving out owl:AnnotationProperty owl:OntologyProperty
    # These are needed for semantic reasons in ontology construction, not to describe data
    OPTIONAL{ 
        ?term rdfs:label ?label.
        FILTER(LCASE(LANG(?label)) = 'en')
    }
    OPTIONAL { ?term skos:definition ?definition .
    FILTER(lang(?definition) = 'en') }
    # OPTIONAL { ?term iao:IAO_0000115 ?definition . }

    FILTER (!isBlank(?term))
    FILTER (!CONTAINS(STR(?term), 'http://www.w3.org/2001/XMLSchema#' ))
    FILTER (!CONTAINS(STR(?term), 'http://www.w3.org/1999/02/22-rdf-syntax-ns#' ))
    FILTER (!CONTAINS(STR(?term), 'http://www.w3.org/2000/01/rdf-schema#' ))
    FILTER (!CONTAINS(STR(?term), 'http://www.w3.org/XML/1998/namespace' ))
}
ORDER BY ?term
